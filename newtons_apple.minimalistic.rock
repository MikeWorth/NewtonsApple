bounciness is 0.624
gravity is 9.81
timestep is 0.001
endtime is 2
minusone is nothing
knock minusone down
let gravity be times minusone
comma is 44
cast comma
zero is 0

calculateenergy takes height and velocitya and velocityb
    let gravitational be gravity times height times -1
    half is 0.5
    let kinetic be half times velocitya times velocitya plus half times velocityb times velocityb 
    return gravitational plus kinetic

bounce takes velocity
    if velocity is lower than zero
        let velocity be times minusone

    return velocity times bounciness

x is 120
y is 121
cast x
cast y

rock columntitles with "time"
rock columntitles with x (this time round it's literally the string 'x')
rock columntitles with y (this time round it's literally the string 'y')
join columntitles with comma

say "Enter height"
x is 0
listen to y
cast y
say "Enter horizontal velocity"
listen to velx
cast velx
vely is 0
let startingenergy be calculateenergy taking y , velx , vely
time is 0

say columntitles

while true
    let energy be calculateenergy taking y,velx,vely
    rock outputline
    let outputline at zero be time
    rock outputline with x
    rock outputline with y
    join outputline with comma
    say outputline
    outputline is empty
    let thistimestep be timestep
    if y is lower than timestep times vely times minusone
        let thistimestep be y over vely
        let velx be bounce taking velx
        let vely be bounce taking vely
    
    let time be plus thistimestep
    let x be plus thistimestep times velx
    let y be plus thistimestep times vely
    let vely be plus thistimestep times gravity
    if startingenergy over 1000 is greater than energy
        break

    if time is as high as endtime
        break



